rootProject.name = 'Strata'

List<String> modules = [
        "basics",
        "basics-testutils",
        "collect",
		"collect-testutils",
        "curve",
        "engine",
        "finance",
        "finance-beta",
        "finance-testutils",
        "function",
        "function-beta",
        "market",
        "pricer",
        "pricer-beta",
        "pricer-testutils",
        "source"
]

modules.each { m ->
        include m
        project(":${m}").projectDir = file("modules/${m}")
}

include "examples"

if(System.properties['analytics.ref'])
{
        include "analytics"
        project(":analytics").projectDir = file "modules/analytics"

        String sourceBuildscriptSource = this.class.getResource("/analytics.gradle").text
        File targetBuildscriptFile = new File(project(":analytics").projectDir, "build.gradle")
        targetBuildscriptFile.parentFile.mkdirs()
        targetBuildscriptFile.text = """\
/**
 * Modifications to this file will be overwritten
 * Make changes in \$projectRoot/buildSrc/src/resources/analytics.gradle instead
 */

${sourceBuildscriptSource}
"""
}
